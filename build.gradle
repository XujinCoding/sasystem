description 'sa项目(父工程)'

//构建Gradle脚本自身需要的资源，可以声明的资源包括依赖项、第三方插件、maven仓库地址等。
buildscript {
    ext {
        springBootVersion = '2.2.1.RELEASE'
        springCloudVersion = 'Hoxton.RELEASE'
        springCloudAlibabaVersion = '0.2.2.RELEASE'
    }

    //设置仓库
    repositories {
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://repo.spring.io/milestone'}
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

//配置全局, 包括root项目和子项目
allprojects {
    group 'com.sa'
    version '1.0-SNAPSHOT'


    //配置编码格式
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    //设置仓库
    repositories {
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://repo.spring.io/milestone'}
    }


}

apply from: 'version.gradle'

//配置所有子项目
subprojects {

    apply plugin: 'java'
    apply plugin: 'java-library' //api
    apply plugin: 'io.spring.dependency-management'
    sourceCompatibility= 11
    targetCompatibility= 11


    //公用的依赖
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-jersey:2.7.2'
        implementation 'org.glassfish.jersey.core:jersey-server:2.29.1'
        api 'io.github.openfeign:feign-jaxrs2:10.4.0'
        annotationProcessor 'org.projectlombok:lombok:1.18.2'
        implementation 'io.swagger:swagger-jaxrs:1.6.2'
        compileOnly 'org.projectlombok:lombok:1.18.12'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.2'
        testCompileOnly 'org.projectlombok:lombok:1.18.2'

    }

    test {
        useJUnitPlatform()
    }


    // dependencyManagement版本统一管理，类似于父maven的dependencyManagement
    dependencyManagement {
        dependencies {
            for(depJar in rootProject.ext.dependencies){
                dependency depJar.value
            }
        }
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
        }
    }
}



project(":platform:common"){
    description("公共模块:存放微服务常用的工具类")
    //依赖
    dependencies {
        api 'com.alibaba:fastjson'

        api 'io.springfox:springfox-swagger2'
        api 'io.springfox:springfox-swagger-ui'
        api 'io.jsonwebtoken:jjwt'
        api 'org.springframework.cloud:spring-cloud-starter-openfeign'
//        api 'org.springframework.cloud:spring-cloud-starter-alibaba-sentinel'

    }
}

project(":service-web:customer-ui"){
    description("客户模块")
    apply plugin: 'org.springframework.boot'
    apply plugin : 'java-library'
    apply plugin: 'org.springframework.boot'

    dependencies {
        api 'org.springframework.boot:spring-boot-starter-web'
        api project(':platform:common')
    }
}


